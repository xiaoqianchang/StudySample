plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def releaseTime() {
    return new Date().format("yyyyMMdd_HHmm", TimeZone.getTimeZone("GMT+08"))
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        multiDexEnabled true
        flavorDimensions "versionCode"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../platform.jks")
            storePassword "666666"
            keyAlias "beantechs-dev"
            keyPassword "666666"
        }
    }

    buildTypes {
        release {
            buildConfigField("String", "releaseTime", "\"" + releaseTime() + "\"")
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField("String", "releaseTime", "\"" + releaseTime() + "\"")
            minifyEnabled false
            versionNameSuffix 'dev'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        offical {}
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def versionName = defaultConfig.versionName
            def versionCode = defaultConfig.versionCode
            def productFlavor = variant.productFlavors[0].name
            def suffix = ".apk"

            outputFileName = "TBoxNetDemo-" +
                    "v${versionName}-" +
                    "${versionCode}-" +
                    "${productFlavor}-" +
                    "${releaseTime()}" +
                    suffix
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {

    def dependencies = rootProject.ext.dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation dependencies.core_ktx
    implementation dependencies.appcompat
    implementation dependencies.material
    implementation dependencies.constraintlayout
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}